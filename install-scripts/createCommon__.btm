REM #input arguments
REM # %1 - PKG_TYPE - kind of installation (snapshot, release) - needed by the compiler to distinguish binaries
REM # %2 - THIRD_PARTY_DIR - folder with 3rd party software to be included in the installation package
REM # %3 - PURE_ORANGE - name of package with pure Orange
REM # %4 - ORANGE_W_PYTHON - name of package that includes Python installer

set PKG_TYPE=%1
set THIRD_PARTY_DIR=%2
set PURE_ORANGE=%3
set ORANGE_W_PYTHON=%4

cdd %TMPDIR\source
call _pyxtract.bat

for %pyver in (%PYTHONVERSIONS) do (
  echo *************************************
  echo *************************************
  echo *** Compiling for Python %pyver

  set npver=%@LEFT[1,%pyver].%@RIGHT[-1,%pyver]
  cdd %TMPDIR/source
  set COMPILELOG=%TMPDIR\win-%PKG_TYPE-core-compile-%npver.log
  del /q %COMPILELOG
  set PYTHON=%PYTHONBASE%%pyver
  set OBJPOSTFIX=-%PKG_TYPE%
  REM # we expect Python to be on same drive
  set PYORANGEDIR=%PYTHON\lib\site-packages\orange
  set PARTY=%SCRIPTDIR\%THIRD_PARTY_DIR\%pyver

  if direxist %PARTY (
    REM # compile
    pushd
    cdd %PYORANGEDIR
    del /q orange.pyd corn.pyd statc.pyd orangene.pyd orangeom.pyd
    popd
    vcexpress orange.sln /build Release /out %COMPILELOG
    break_on_error

    REM # compress pyd and create stamps_pyd.txt, needed for update from web (accessed by updateFromSVN.bat)
    set WEBBINDIR=%DOWNLOADDIR\binaries\%pyver
    if not direxist %WEBBINDIR mkdir /s %WEBBINDIR
 
    cdd %PYORANGEDIR
    except (*_d.pyd) for %pydf in (*.pyd) do (
      del /q %TMPDIR\orange\%pydf
      upx %pydf -o %TMPDIR/orange/%pydf
	  copy %TMPDIR/orange/%pydf %WEBBINDIR
    )

    cdd %WEBBINDIR
    del /q stamps_pyd.txt
    for %pydf in (*.pyd) do (
      set MD5=%@MD5[%pydf]
      echo %pydf %MD5 >> stamps_pyd.txt
    )
	
	REM # pack
    cdd %SCRIPTDIR
    set COMMON_NSI_OPTIONS=/DORANGEDIR=%TMPDIR\orange /DPYVER=%pyver /DNPYVER=%npver /DPARTY=%PARTY /DQTVER=%@SUBSTR[%THIRD_PARTY_DIR,2,2] install3.nsi
    nsis /O%TMPDIR/win-%PKG_TYPE-core-packWithNSIS-%THIRD_PARTY_DIR-py%npver.log /DOUTFILENAME="%TMPDIR\%PURE_ORANGE-py%npver.exe" %COMMON_NSI_OPTIONS %+
    break_on_error
    nsis /O%TMPDIR/win-%PKG_TYPE-core-packWithNSIS-%THIRD_PARTY_DIR-w-python-py%npver.log /DCOMPLETE /DOUTFILENAME="%TMPDIR\%ORANGE_W_PYTHON-py%npver.exe" %COMMON_NSI_OPTIONS
    break_on_error
  ) else (
    echo skipped because no 3rd-party software available
  )
)

rem # compile documentation
REM cdd %SCRIPTDIR/doc
REM call compileDocumentation.btm
REM cdd %WEBDOCDIR
REM winrar a %DOWNLOADDIR\orange-chm.zip *.chm
REM break_on_error
